/*
The file encoding is UTF-8 without BOM; the newline format is LF
This file is part of "This Is the House That Jack Built" for UrqW.
SPDX-License-Identifier: CC0-1.0
*/

:ru_v0_0
perkill
urqw_game_lang="ru"
urqw_title="Дом, который построил Джек"
pln <h1>#urqw_title$</h1>
image_caption="Классическая иллюстрированная обложка издания 1878 года"
image="cover.jpg|Джек с семьёй на фоне дома"
pln Кумулятивная интерактивная литература
pln Разработка: Никита Цейковец
pln Иллюстрация: Рэндольф Кальдекотт 
pln Перевод с английского: Самуил Маршак (1923)
next="Далее"
back="Назад"
check="Проверить"
insert_msg="Введите замену для "
last=9
words=9
рис="***"
крыс="****"
кот="***"
пёс="***"
коров="*****"
пса="***"
девиц="*****"
пастух="******"
петуха="******"
label=urqw_game_lang + "_v0_"
start_label=label + 1
btn #start_label$, #next$
end

:ru_v0_1
number=1
pln Вот дом,
pln Который построил Джек.
proc buttons
end

:ru_v0_2
number=2
pln Вот [[#рис$|insert_var="рис" & goto insert]],
pln Который был спрятан в чулане от [[#крыс$|insert_var="крыс" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0_3
number=3
pln Вот несколько [[#крыс$|insert_var="крыс" & goto insert]],
pln Забравшихся в [[#рис$|insert_var="рис" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0_4
number=4
pln Вот [[#кот$|insert_var="кот" & goto insert]],
pln Напавший на [[#крыс$|insert_var="крыс" & goto insert]],
pln Забравшихся в [[#рис$|insert_var="рис" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0_5
number=5
pln Вот [[#пёс$|insert_var="пёс" & goto insert]] без хвоста,
pln Который пугает и треплет [[#кот$|insert_var="кот" & goto insert]]а,
pln Поймавшего [[#крыс$|insert_var="крыс" & goto insert]],
pln Забравшихся в [[#рис$|insert_var="рис" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0_6
number=6
pln А это - [[#коров$|insert_var="коров" & goto insert]]а безрогая,
pln Лягнувшая старого [[#пса$|insert_var="пса" & goto insert]] без хвоста,
pln Который пугает и треплет [[#кот$|insert_var="кот" & goto insert]]а,
pln Поймавшего [[#крыс$|insert_var="крыс" & goto insert]],
pln Забравшихся в [[#рис$|insert_var="рис" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0_7
number=7
pln А это - [[#девиц$|insert_var="девиц" & goto insert]]а, худая и строгая,
pln Которая доит [[#коров$|insert_var="коров" & goto insert]]у безрогую,
pln Лягнувшую старого [[#пса$|insert_var="пса" & goto insert]] без хвоста,
pln Который пугает и треплет [[#кот$|insert_var="кот" & goto insert]]а,
pln Поймавшего [[#крыс$|insert_var="крыс" & goto insert]],
pln Забравшихся в [[#рис$|insert_var="рис" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0_8
number=8
pln А это - ленивый и толстый [[#пастух$|insert_var="пастух" & goto insert]],
pln Который бранится с [[#девиц$|insert_var="девиц" & goto insert]]ей строгою,
pln Которая доит [[#коров$|insert_var="коров" & goto insert]]у безрогую,
pln Лягнувшую старого [[#пса$|insert_var="пса" & goto insert]] без хвоста,
pln Который пугает и треплет [[#кот$|insert_var="кот" & goto insert]]а,
pln Поймавшего [[#крыс$|insert_var="крыс" & goto insert]],
pln Забравшихся в [[#рис$|insert_var="рис" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0_9
number=9
pln Вот два [[#петуха$|insert_var="петуха" & goto insert]],
pln Которые будят того [[#пастух$|insert_var="пастух" & goto insert]]а,
pln Который бранится с [[#девиц$|insert_var="девиц" & goto insert]]ей строгою,
pln Которая доит [[#коров$|insert_var="коров" & goto insert]]у безрогую,
pln Лягнувшую старого [[#пса$|insert_var="пса" & goto insert]] без хвоста,
pln Который пугает и треплет [[#кот$|insert_var="кот" & goto insert]]а,
pln Поймавшего [[#крыс$|insert_var="крыс" & goto insert]],
pln Забравшихся в [[#рис$|insert_var="рис" & goto insert]],
pln В доме,
pln Который построил Джек.
proc buttons
end

:ru_v0__check
correct=0
javascript="'" + рис + "'.toLowerCase();"
if javascript = "рис" then correct = correct + 1
javascript="'" + крыс + "'.toLowerCase();"
if javascript = "крыс" then correct = correct + 1
javascript="'" + кот + "'.toLowerCase();"
if javascript = "кот" then correct = correct + 1
javascript="'" + пёс + "'.toLowerCase();"
if javascript = "пёс" or javascript = "пес" then correct = correct + 1
javascript="'" + коров + "'.toLowerCase();"
if javascript = "коров" then correct = correct + 1
javascript="'" + пса + "'.toLowerCase();"
if javascript = "пса" then correct = correct + 1
javascript="'" + девиц + "'.toLowerCase();"
if javascript = "девиц" or javascript = "молочниц" then correct = correct + 1
javascript="'" + пастух + "'.toLowerCase();"
if javascript = "пастух" then correct = correct + 1
javascript="'" + петуха + "'.toLowerCase();"
if javascript = "петуха" then correct = correct + 1
if correct = words then pln Всё верно. Это победа! & quit
if correct = 0 then pln У вас 0 правильных вариантов. Это провал!
if correct > 0 then javascript="window.percentageRatio(" + correct + ", " + words + ");"
if correct > 0 then pln У вас #javascript$% правильных вариантов. Надо ещё поработать!
current_label=label + number
btn #current_label$, Продолжить
end
